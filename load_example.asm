; Thomson MO5 Disk Utils by Orion_ [2024]

CALL	MACRO
	SWI
	FCB	&1
	ENDM

DISK	EQU	$26	; System SWI CALLs

; Disk monitor variables
DK_READ	EQU	$02	; CMD
DK_CMD	EQU	$2048
DK_DRV	EQU	$2049
DK_TRK	EQU	$204A
DK_SECT	EQU	$204C
DK_BUF	EQU	$204F
;------->

	CLR	<DK_DRV		; DiskInit
	CLR	<DK_TRK
	LDA	#DK_READ
	STA	<DK_CMD

	; "S/F_test_bin" defines are generated by the tool fd_boot_maker, this is an example on how to use them using the disk loading routine below
	LDA	#S_test_bin	; Size / 256
	LDX	#F_test_bin	; Track/Sector
	LDY	#$3000		; Buffer
	BSR	LoadDisk

	JMP	$3000

;------->

	; X = Track / Sector number
	; Y = Buffer
	; A = Number of sectors (size of data / 256)
LoadDisk:
	STX	<DK_TRK+1	; Store Track/Sector in 16bits because DK_SECT is following DK_TRK+1
	STY	<DK_BUF		; Buffer Address
	LDB	<DK_SECT	; B = Cur Sector (for later CMPB)
	ADDA	<DK_BUF		; A = Size + Buffer Start Address -> to get Buffer End Address
.load:
				; Read One Sector
	CALL	DISK
	CMPB	#16		; 16 Sector per Track
	BNE	.NextSector
	CLRB			; Reset Sector to 1
	INC	<DK_TRK+1	; Next Track
.NextSector:
	INCB			; Next Sector
	STB	<DK_SECT
	INC	<DK_BUF		; Buffer += 256
	CMPA	<DK_BUF		; Check if end of buffer
	BNE	.load		; Else load next sector
	RTS

;------->

